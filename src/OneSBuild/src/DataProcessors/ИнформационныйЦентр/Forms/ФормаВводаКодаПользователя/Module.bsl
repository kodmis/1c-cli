#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь(); 
	Email = ПользователиСлужебный.ОписаниеПользователя(ТекущийПользователь).АдресЭлектроннойПочты;
	ИмяПользователя = ПользователиСлужебный.ОписаниеПользователя(ТекущийПользователь).Наименование;
	
	Элементы.НадписьВведитеКод.Заголовок = НСтр("ru = 'Введите код пользователя для работы с обращениями.
		|Этот код мог быть отправлен на ваш e-mail [email].'");
	
	Элементы.НадписьВведитеКод.Заголовок = СтрЗаменить(Элементы.НадписьВведитеКод.Заголовок, "[email]", Email);
	
	АдресУСП = ИнформационныйЦентрСервер.АдресВнешнегоАнонимногоИнтерфейсаУСП();
	
	СведенияОбИБ = ИнформационныйЦентрСервер.СведенияОбИБДляИнтеграции();
	ИмяКонфигурации = СведенияОбИБ.ИмяКонфигурации;
	ВерсияКонфигурации = СведенияОбИБ.ВерсияКонфигурации;
	ВерсияПлатформы = СведенияОбИБ.ВерсияПлатформы;
	ИдентификаторКлиента = СведенияОбИБ.ИдентификаторКлиента;
	ИдентификаторИБ = СведенияОбИБ.ИдентификаторИБ;
	
	ДанныеНастроекИнтеграцииСУСП = ИнформационныйЦентрСервер.ДанныеНастроекИнтеграцииСУСП();
	КодРегистрацииВУСП = ДанныеНастроекИнтеграцииСУСП.КодРегистрацииВУСП;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапроситьКод(Команда)
	
	ШаблонАдресаСтраницы = 
		"%1/v1/RequestUserCode?email=%2&appname=%3&appversion=%4&platf=%5&ibid=%6&clid=%7&code=%8&username=%9";
	СсылкаНаСтраницу = СтрШаблон(ШаблонАдресаСтраницы, 
		АдресУСП, Email, ИмяКонфигурации, ВерсияКонфигурации, 
		ВерсияПлатформы, ИдентификаторИБ, ИдентификаторКлиента, КодРегистрацииВУСП, ИмяПользователя);
		
	#Если ВебКлиент Тогда
		ПерейтиПоНавигационнойСсылке(СсылкаНаСтраницу);
	#Иначе
		ЗапуститьПриложение(СсылкаНаСтраницу);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКод(Команда)
	
	РезультатПроверки = ВвестиКодНаСервере();
	
	Если РезультатПроверки.КодВерный Тогда
		ИнформационныйЦентрКлиент.СохранитьКодПользователяНаКомпьютере(КодПользователя);
		ИнформационныйЦентрКлиент.ОткрытьСписокОбращенийВУСП(КодПользователя);
		Закрыть();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатПроверки.ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВвестиКодНаСервере()
	
	РезультатПроверки = ИнформационныйЦентрСервер.ПроверитьКодПользователя(КодПользователя, Email);
	
	Если Не РезультатПроверки.КодВерный Тогда
		ЗафиксироватьОшибку(РезультатПроверки.ТекстСообщения);
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат ИнформационныйЦентрСервер.ПолучитьИмяСобытияДляЖурналаРегистрации();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗафиксироватьОшибку(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(
		СтрШаблон("%1.%2", ИмяСобытияЖурналаРегистрации(), 
			НСтр("ru = 'Ввод кода пользователя'", ОбщегоНазначения.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстОшибки);
	
КонецПроцедуры

#КонецОбласти
