#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ОписаниеПеременных

Перем ТекущийКонтейнер; // ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера
Перем ТекущееИмяХранилищаНастроек;
Перем ТекущееХранилищеНастроек;
Перем ТекущиеОбработчики;
Перем ТекущийПотокЗаменыСсылок;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(Контейнер, ИмяХранилищаНастроек, Обработчики, ПотокЗаменыСсылок) Экспорт
	
	ТекущийКонтейнер = Контейнер;
	ТекущиеОбработчики = Обработчики;
	ТекущийПотокЗаменыСсылок = ПотокЗаменыСсылок;
	
	ТекущееИмяХранилищаНастроек = ИмяХранилищаНастроек;
	ТекущееХранилищеНастроек = ОбщегоНазначения.ВычислитьВБезопасномРежиме(ТекущееИмяХранилищаНастроек);
	
КонецПроцедуры

// Загружает настройки пользователей.
//
// Параметры:
//   СопоставлениеПользователей - Соответствие - коллекция пользователей, настройки которых нужно загрузить: 
//                                     Если не указано, то загружаются все настройки "как есть".
//                                       *Ключ - старое имя пользователя
//                                       *Значение - новое имя пользователя.
Процедура ЗагрузитьДанные(СопоставлениеПользователей = Неопределено) Экспорт
	
	Отказ = Ложь;
	ТекущиеОбработчики.ПередЗагрузкойХранилищаНастроек(
		ТекущийКонтейнер,
		ТекущееИмяХранилищаНастроек,
		ТекущееХранилищеНастроек,
		Отказ);
	
	Если Не Отказ Тогда
		
		ЗагрузитьНастройкиВСтандартноеХранилище(СопоставлениеПользователей);
		
	КонецЕсли;
	
	ТекущиеОбработчики.ПослеЗагрузкиХранилищаНастроек(
		ТекущийКонтейнер,
		ТекущееИмяХранилищаНастроек,
		ТекущееХранилищеНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗагрузитьНастройкиВСтандартноеХранилище(СопоставлениеПользователей)
	
	ОписанияНастроек = ТекущийКонтейнер.ПолучитьОписанияФайловИзКаталога(
		ВыгрузкаЗагрузкаДанныхСлужебный.UserSettings(), ТекущееИмяХранилищаНастроек);
	
	Для Каждого ОписаниеФайла Из ОписанияНастроек Цикл
		
		ТекущийКонтейнер.РаспаковатьФайл(ОписаниеФайла);
		
		ОшибкиЗагрузкиНастроекХранилища = Новый Массив();
		
		Попытка
	    	ТекущийПотокЗаменыСсылок.ВыполнитьЗаменуСсылокВФайле(ОписаниеФайла);
			ЗаменаСсылокВыполнена = Истина;
		Исключение
			ОшибкиЗагрузкиНастроекХранилища.Добавить(
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаменаСсылокВыполнена = Ложь;
		КонецПопытки;
			
		Если ЗаменаСсылокВыполнена Тогда
			ПотокЧтения = Обработки.ВыгрузкаЗагрузкаДанныхПотокЧтенияДанныхИнформационнойБазы.Создать();
			ПотокЧтения.ОткрытьФайл(ОписаниеФайла.ПолноеИмя, Истина);

			Пока ПотокЧтения.ПрочитатьОбъектДанныхИнформационнойБазы() Цикл

				Отказ = Ложь;

				ЗаписьНастроек = ПотокЧтения.ТекущийОбъект(); // См. ВыгрузкаЗагрузкаДанныхСлужебный.НоваяЗаписьНастроек 
				Артефакты = ПотокЧтения.АртефактыТекущегоОбъекта();

				КлючНастроек = ЗаписьНастроек.КлючНастроек;
				КлючОбъекта = ЗаписьНастроек.КлючОбъекта;
				Пользователь = ЗаписьНастроек.Пользователь;
				Представление = ЗаписьНастроек.Представление;

				Если СопоставлениеПользователей <> Неопределено Тогда
					Пользователь = СопоставлениеПользователей.Получить(Пользователь);
					Если Не ЗначениеЗаполнено(Пользователь) Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				СериализацияЧерезХранилищеЗначения = ЗаписьНастроек.СериализацияЧерезХранилищеЗначения;
				
				Если СериализацияЧерезХранилищеЗначения Тогда
					Настройки = ЗаписьНастроек.Настройки.Получить();
				Иначе
					Настройки = ЗаписьНастроек.Настройки;
				КонецЕсли;

				ТекущиеОбработчики.ПередЗагрузкойНастроек(
					ТекущийКонтейнер,
					ТекущееИмяХранилищаНастроек,
					КлючНастроек,
					КлючОбъекта,
					Настройки,
					Пользователь,
					Представление,
					Артефакты,
					Отказ);

				Если Не Отказ Тогда

					ОписаниеНастроек = Новый ОписаниеНастроек;
					ОписаниеНастроек.Представление = Представление;

					ТекущееХранилищеНастроек.Сохранить(
						КлючОбъекта,
						КлючНастроек,
						Настройки,
						ОписаниеНастроек,
						Пользователь);

				КонецЕсли;

				ТекущиеОбработчики.ПослеЗагрузкиНастроек(
					ТекущийКонтейнер,
					ТекущееИмяХранилищаНастроек,
					КлючНастроек,
					КлючОбъекта,
					Настройки,
					Пользователь,
					Представление,
					Артефакты);

			КонецЦикла;

			ПотокЧтения.Закрыть();
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				ОшибкиЗагрузкиНастроекХранилища,
				ПотокЧтения.Ошибки());
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОшибкиЗагрузкиНастроекХранилища) Тогда

			КраткоеПредупреждение = СтрШаблон(
				НСтр("ru = 'В процессе загрузки хранилища ''%1'' обнаружены ошибки, загрузка части настроек пропущена'"),
				ТекущееИмяХранилищаНастроек);
			
			ТекущийКонтейнер.ДобавитьПредупреждение(КраткоеПредупреждение);	
			
			ЧастиТекстаПредупреждения = Новый Массив;	
			ЧастиТекстаПредупреждения.Добавить(КраткоеПредупреждение);		
			ЧастиТекстаПредупреждения.Добавить(Символы.ПС);

			Для Каждого ОшибкаЗагрузкиНастроекХранилища Из ОшибкиЗагрузкиНастроекХранилища Цикл
				ЧастиТекстаПредупреждения.Добавить("● ");
				ЧастиТекстаПредупреждения.Добавить(ОшибкаЗагрузкиНастроекХранилища);
				ЧастиТекстаПредупреждения.Добавить(Символы.ПС);
			КонецЦикла;
						
			ИмяСобытия = НСтр("ru = 'Загрузка настроек пользователей. Загрузка части настроек пропущена'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Ошибка,,, 
				СтрСоединить(ЧастиТекстаПредупреждения));
				
		КонецЕсли;
					
		УдалитьФайлы(ОписаниеФайла.ПолноеИмя);	
			
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли
