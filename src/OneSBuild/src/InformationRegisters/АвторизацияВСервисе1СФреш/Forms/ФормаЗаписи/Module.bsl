#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи.Пользователь) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ПарольВХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			РегистрыСведений.АвторизацияВСервисе1СФреш.ВладелецБезопасногоХранилища(Запись.ИсходныйКлючЗаписи.Пользователь));
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ЗначениеЗаполнено(ПарольВХранилище) Тогда
			Пароль = Новый УникальныйИдентификатор;			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Пользователь) Тогда
		Запись.Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПарольИзменен Тогда
		Владелец = РегистрыСведений.АвторизацияВСервисе1СФреш.ВладелецБезопасногоХранилища(Запись.Пользователь);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, Пароль);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

#КонецОбласти 