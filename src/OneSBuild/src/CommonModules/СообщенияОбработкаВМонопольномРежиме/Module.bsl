
#Область ПрограммныйИнтерфейс

// См. ОбменСообщениямиПереопределяемый.ПолучитьОбработчикиКаналовСообщений.
//
Процедура ПриОпределенииОбработчиковКаналовСообщений(Обработчики) Экспорт
	
	ДобавитьОбработчикКаналаСообщений("МонопольныйРежим\УстановитьЧасовойПоясИБ", СообщенияОбработкаВМонопольномРежиме, Обработчики);
	
КонецПроцедуры // ПолучитьОбработчикиКаналовСообщений()

// Выполняет обработку тела сообщения из канала в соответствии с алгоритмом текущего канала сообщений.
//
// Параметры:
//  КаналСообщений - Строка - идентификатор канала сообщений, из которого получено сообщение.
//  ТелоСообщения  - Произвольный - тело сообщения, полученное из канала, которое подлежит обработке.
//  Отправитель    - ПланОбменаСсылка.ОбменСообщениями - конечная точка, которая является отправителем сообщения.
//
Процедура ОбработатьСообщение(КаналСообщений, ТелоСообщения, Отправитель) Экспорт
	
	Если КаналСообщений = "МонопольныйРежим\УстановитьЧасовойПоясИБ" Тогда
		УстановитьЧасовойПоясИБ(ТелоСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает новый часовой пояс информационной базы
//
// Параметры:
//  Сообщение - Структура,
//
Процедура УстановитьЧасовойПоясИБ(Сообщение) Экспорт
	
	ЧасовойПояс = Сообщение.ЧасовойПояс;
	ОбластьДанных = Сообщение.ОбластьДанных;
	
	Если РаботаВМоделиСервисаПовтИсп.ЭтоРазделеннаяКонфигурация() Тогда
		РаботаВМоделиСервиса.ВойтиВОбластьДанных(ОбластьДанных);
	КонецЕсли;
	
	Если ПолучитьЧасовойПоясИнформационнойБазы() <> ЧасовойПояс Тогда
		
		ВнешнийМонопольныйРежим = МонопольныйРежим();
		
		Если ВнешнийМонопольныйРежим Тогда
			
			ОбластьЗаблокирована = Истина;
			
		Иначе
			
			Попытка
				
				УстановитьМонопольныйРежим(Истина);
				ОбластьЗаблокирована = Истина;
				
			Исключение
				
				ОбластьЗаблокирована = Ложь;
				
				ШаблонСообщения = НСтр("ru = 'Не удалось заблокировать область данных для установки часового пояса ""%1""'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ЧасовойПояс);
				ЗаписьЖурналаРегистрации(СообщенияУдаленногоАдминистрированияРеализация.СобытиеЖурналаРегистрацииУдаленноеАдминистрированиеУстановитьПараметры(),
					УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
				
			КонецПопытки;
			
		КонецЕсли;
		
		Если ОбластьЗаблокирована Тогда
			
			Если ЗначениеЗаполнено(ЧасовойПояс) Тогда
				
				УстановитьЧасовойПоясИнформационнойБазы(ЧасовойПояс);
				
			Иначе
				
				УстановитьЧасовойПоясИнформационнойБазы();
				
			КонецЕсли;
			
			Если НЕ ВнешнийМонопольныйРежим Тогда
				
				УстановитьМонопольныйРежим(Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РаботаВМоделиСервисаПовтИсп.ЭтоРазделеннаяКонфигурация() Тогда
		РаботаВМоделиСервиса.ВыйтиИзОбластиДанных();
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОбработчикКаналаСообщений(Канал, ОбработчикКанала, Обработчики)
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Канал = Канал;
	Обработчик.Обработчик = ОбработчикКанала;
	
КонецПроцедуры

#КонецОбласти