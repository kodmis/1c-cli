#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значение перечисления по имени значения свойства программного интерфейса сервиса.
//
// Параметры:
//  ИмяЗначения - Строка - имя значения свойства.
// 
// Возвращаемое значение:
//   ПеречислениеСсылка.ПериодичностьПериодовДействия - значение перечисления.
//
Функция ЗначениеПоИмени(ИмяЗначения) Экспорт
    
    Если ИмяЗначения = "day" Тогда
        Возврат День;
    ИначеЕсли ИмяЗначения = "week" Тогда
        Возврат Неделя;
    ИначеЕсли ИмяЗначения = "decade" Тогда
        Возврат Декада;
    ИначеЕсли ИмяЗначения = "month" Тогда
        Возврат Месяц;
    ИначеЕсли ИмяЗначения = "semester" Тогда
        Возврат Квартал;
    ИначеЕсли ИмяЗначения = "half_year" Тогда
        Возврат Полугодие;
    ИначеЕсли ИмяЗначения = "year" Тогда
        Возврат Год;
    Иначе
        Возврат ПустаяСсылка();
    КонецЕсли; 
    
КонецФункции

// Добавляет к дате указанное число периодов действия.
//
// Параметры:
//  ИсходнаяДата - Дата - исходная дата
//  Периодичность - ПеречислениеСсылка.ПериодичностьПериодовДействия - периодичность периода
//  Количество - Число - количество добавляемых периодов
// 
// Возвращаемое значение:
//  Дата - результирующая дата 
//
Функция ДобавитьКДате(Знач ИсходнаяДата, Знач Периодичность, Знач Количество) Экспорт 
	
	Дата = ИсходнаяДата;
	
	ПериодНачалоДня = НачалоДня(ИсходнаяДата);
	
	КоличествоСекундВСутках = 86400;
	КоличествоДнейВНеделе = 7;
	КоличествоДнейВДекаде = 10;
	КоличествоМесяцевВКвартале = 3;
	КоличествоМесяцевВПолугодии = 6;
	КоличествоМесяцевВГоду = 12;
	
	ДанныеПериода = Новый Структура("Периодичность, Количество", Периодичность, Количество);
	
	Если ДанныеПериода.Периодичность = День
			ИЛИ ДанныеПериода.Периодичность = Декада
			ИЛИ ДанныеПериода.Периодичность = Неделя Тогда
			
		КоличествоСутокВСекундах = ДанныеПериода.Количество * КоличествоСекундВСутках;
		
		Если ДанныеПериода.Периодичность = Декада Тогда
			КоличествоСутокВСекундах = КоличествоСутокВСекундах * КоличествоДнейВДекаде;
		ИначеЕсли ДанныеПериода.Периодичность = Неделя Тогда
			КоличествоСутокВСекундах = КоличествоСутокВСекундах * КоличествоДнейВНеделе;
		КонецЕсли;
		
		Дата = ПериодНачалоДня + КоличествоСутокВСекундах;
		
	Иначе
		КоличествоМесяцев = ДанныеПериода.Количество;
		
		Если ДанныеПериода.Периодичность = Квартал Тогда
			КоличествоМесяцев = КоличествоМесяцев * КоличествоМесяцевВКвартале;
		ИначеЕсли ДанныеПериода.Периодичность = Полугодие Тогда
			КоличествоМесяцев = КоличествоМесяцев * КоличествоМесяцевВПолугодии;
		ИначеЕсли ДанныеПериода.Периодичность = Год Тогда
			КоличествоМесяцев = КоличествоМесяцев * КоличествоМесяцевВГоду;
		КонецЕсли;
		
		Дата = ДобавитьМесяц(ПериодНачалоДня, КоличествоМесяцев);
		
	КонецЕсли;
	
	Возврат Дата - 1;
	
КонецФункции

#КонецОбласти

#КонецЕсли