//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает значение контекста
// 
// Параметры:
//  ИмяЗначения - Строка
// 
// Возвращаемое значение:
//  Произвольный - Сохраненное в контексте значение
Функция Значение(ИмяЗначения) Экспорт
	
	Уровень = ЮТКонтекст.КонтекстИсполнения().Уровень;
	Уровни = ЮТФабрика.УровниИсполнения();
	
	КонтекстТеста = ЮТКонтекст.КонтекстТеста();
	Если Уровень = Уровни.Тест И КонтекстТеста <> Неопределено И КонтекстТеста.Свойство(ИмяЗначения) Тогда
		
		Возврат КонтекстТеста[ИмяЗначения];
		
	КонецЕсли;
	
	КонтекстНабора = ЮТКонтекст.КонтекстНабора();
	Если (Уровень = Уровни.НаборТестов ИЛИ Уровень = Уровни.Тест)
		И КонтекстНабора <> Неопределено И КонтекстНабора.Свойство(ИмяЗначения) Тогда
			
		Возврат КонтекстНабора[ИмяЗначения];
		
	КонецЕсли;
	
	КонтекстМодуля = ЮТКонтекст.КонтекстМодуля();
	Если (Уровень = Уровни.Модуль ИЛИ Уровень = Уровни.Тест ИЛИ Уровень = Уровни.НаборТестов)
		И КонтекстМодуля <> Неопределено И КонтекстМодуля.Свойство(ИмяЗначения) Тогда
			
		Возврат КонтекстМодуля[ИмяЗначения];
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Установить значение.
//  Устанавливает значение в контекст тестов.
//  Установка происходит в соответствии с уровнем исполнения
// Параметры:
//  ИмяЗначения - Строка
//  Значение - Произвольный
Процедура УстановитьЗначение(ИмяЗначения, Значение) Экспорт
	
	Уровень = ЮТКонтекст.КонтекстИсполнения().Уровень;
	Уровни = ЮТФабрика.УровниИсполнения();
	
	Контекст = ТекущийКонтекст();
	
	Если Контекст = Неопределено Тогда
		
		Если Уровень = Уровни.Тест Тогда
			ЮТКонтекст.УстановитьКонтекстТеста();
		ИначеЕсли Уровень = Уровни.НаборТестов Тогда
			ЮТКонтекст.УстановитьКонтекстНабораТестов();
		ИначеЕсли Уровень = Уровни.Модуль Тогда
			ЮТКонтекст.УстановитьКонтекстМодуля();
		Иначе
			ВызватьИсключение СтрШаблон("Неизвестный уровень исполнения `%1`", Уровень);
		КонецЕсли;
		
		Контекст = ТекущийКонтекст();
		
	КонецЕсли;
	
	Контекст.Вставить(ИмяЗначения, Значение);
	
КонецПроцедуры

Функция ТекущийКонтекст() Экспорт
	
	Уровень = ЮТКонтекст.КонтекстИсполнения().Уровень;
	Уровни = ЮТФабрика.УровниИсполнения();
	
	Если Уровень = Уровни.Тест Тогда
		
		Контекст = ЮТКонтекст.КонтекстТеста();
		
	ИначеЕсли Уровень = Уровни.НаборТестов Тогда
		
		Контекст = ЮТКонтекст.КонтекстНабора();
		
	ИначеЕсли Уровень = Уровни.Модуль Тогда
		
		Контекст = ЮТКонтекст.КонтекстМодуля();
		
	Иначе
		
		Контекст = Неопределено;
		
	КонецЕсли;
	
	Возврат Контекст;
	
КонецФункции

#КонецОбласти
